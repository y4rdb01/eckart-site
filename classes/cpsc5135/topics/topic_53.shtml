<!doctype HTML public "-//W3C//DTD HTML 3.2//EN">
<html lang="en">
<head>
<title>Dr. J Dana Eckart</title>
<link rev="mail" href="mailto:eckart_dana@columbusstate.edu">
</head>
<body>

<!--
	This is the primary overall style for the web site.
-->
<style>
	A:link    { color: #007a00; text-decoration: underline }
	A:visited { color: #7a0000; text-decoration: none }
	A:hover   { text-decoration: none }
	A:active  { color: #ff0000; text-decoration: none }
	Body	{ background-color: #ffffe5; color: #000000 }
	Body	{ font-size: 12pt }
	Address	{ font-size: 10pt }
	Table	{ font-size: 12pt }
	Th, Td	{ vertical-align: top }
	Th, Td	{ padding: 5px }
</style>

<p style="text-align: center; margin: auto; font-size: 150%">
	<strong>Dr. J Dana Eckart</strong>: Programming Languages (CPSC 5135)
	- NetLogo Example (SRDP)
</p>

<ol>
<li> <a href="../netlogo/srdp.nlogo">Scissors, Rock, Dynamite, Paper</a>
	implements the classic childrens' game with Rock smashing Scissors,
	Dynamite blowing up Rocks, Paper smothering Dynamiate's fuse, and
	Scissors cutting Paper.
<pre><code>
	patches-own [toolType typeTotals]

	to setup			; Define new command procedure "setup"
		set-patch-size 5	; Sets patch size to 5 pixels on a side
		resize-world 0 63 0 63	; Set x and y min/max values
		clear-all		; Makes a "clean" universe
		ask patches [ set toolType (random 4) ]	; Initialize SRDP
		colorize
		reset-ticks		; Sets the global time counter to 0
	end

	to go
		ask patches [
			; Scissors is type 0
			; Rock is type 1
			; Dynamite is type 2
			; Paper is type 3

			set typeTotals [0 0 0 0]
			foreach [toolType] of neighbors [
				set typeTotals replace-item ? typeTotals (1 + item ? typeTotals)
			]
		]

		ask patches [
			if (item (remainder (toolType + 1) 4) typeTotals) >= 3
				[set toolType remainder (toolType + 1) 4]
		]
		colorize
		tick	; Advance the global clock (+1) and update all patches
	end

	to colorize
		ask patches [
			ifelse toolType = 0
				[ set pcolor [255 0 0] ]   ; Scissors are red
			[ ifelse toolType = 1
				[ set pcolor [0 255 0] ]   ; Rocks are green
			[ ifelse toolType = 2
				[ set pcolor [0 255 255] ] ; Dynamite is aqua
				[ set pcolor [0 0 255] ]   ; Paper is blue
			]]
		]
	end
</code></pre>
<li> Test your understanding of the above by answering these
	<a href="../questions/questions_53.html">self-assessment questions</a>.
</ol>

<hr/>
<em>
<a href="mailto:eckart_dana@columbusstate.edu?subject=web_pages" style="float: left">eckart_dana@columbusstate.edu</a>
<a href="/eckart/classes/cpsc5135" style="float: right">CPSC 5135</a>
</em>

</body>
</html>

